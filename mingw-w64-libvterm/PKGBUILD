# Maintainer: Yusuke Sasaki <yusuke.sasaki.nuem@gmail.com>

_realname=libvterm
pkgbase="mingw-w64-${_realname}-git"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=715.e0a3d4d
pkgrel=1
pkgdesc="libvterm (mingw-w64)"
arch=('any')
url="http://github.com/neovim/libvterm"
license=("MIT")
depends=()
makedepends=()
options=('strip')
source=("${_realname}"::"git+https://github.com/neovim/libvterm.git#branch=master"
        CMakeLists.txt)
sha256sums=('SKIP' 'SKIP')

pkgver() {
  cd "${srcdir}/${_realname}"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_realname}"
  cp ../CMakeLists.txt CMakeLists.txt
}

build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -G'MSYS Makefiles' \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      ../${_realname}

  make
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make DESTDIR=${pkgdir} install
}
