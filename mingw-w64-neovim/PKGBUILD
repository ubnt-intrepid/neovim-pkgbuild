# Maintainer: Yusuke Sasaki <yusuke.sasaki.nuem@gmail.com>

_realname=neovim
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.1.6
pkgrel=1
pkgdesc="vim's rebirth for the 21st century (mingw-w64)"
arch=('any')
url="http://neovim.org/"
license=("Custom: Apache 2.0" "Custom: Vim")
depends=("${MINGW_PACKAGE_PREFIX}-gettext"
         "${MINGW_PACKAGE_PREFIX}-jemalloc"
         "${MINGW_PACKAGE_PREFIX}-libuv"
         "${MINGW_PACKAGE_PREFIX}-libiconv"
         "${MINGW_PACKAGE_PREFIX}-msgpack-c")
makedepends=("winpty"
             "${MINGW_PACKAGE_PREFIX}-gettext"
             "${MINGW_PACKAGE_PREFIX}-jemalloc"
             "${MINGW_PACKAGE_PREFIX}-libiconv"
             "${MINGW_PACKAGE_PREFIX}-libvterm"
             "${MINGW_PACKAGE_PREFIX}-lua51-lpeg"
             "${MINGW_PACKAGE_PREFIX}-lua51-bitop"
             "${MINGW_PACKAGE_PREFIX}-lua51-mpack"
             "${MINGW_PACKAGE_PREFIX}-luajit-git"
             "${MINGW_PACKAGE_PREFIX}-msgpack-c")
options=('strip')
source=("${_realname}"::"git+https://github.com/neovim/neovim.git#tag=v${pkgver}"
        0010-fix-pythonx-runtime.patch
        0020-avoid-to-copy-dll.patch)
sha256sums=('SKIP' 'SKIP' 'SKIP')

prepare() {
  cd "${srcdir}/${_realname}"
  patch -p1 -i "${srcdir}/0010-fix-pythonx-runtime.patch"
  patch -p1 -i "${srcdir}/0020-avoid-to-copy-dll.patch"
}

build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}

  export LUA_CPATH=$(cygpath -w ${MINGW_PREFIX}/lib/lua/5.1)\\'?.dll'
  
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -G'MSYS Makefiles' \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DLUA_PRG=${MINGW_PREFIX}/bin/luajit.exe \
      -DWINPTY_INCLUDE_DIR=/usr/include \
      -DENABLE_JEMALLOC=ON \
      ../${_realname}

  make
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make DESTDIR=${pkgdir} install
}
